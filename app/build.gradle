import com.android.ddmlib.DdmPreferences

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.neenbedankt.android-apt'
//Retrolambda plugin
apply plugin: 'me.tatarka.retrolambda'
apply from: 'build.workaround-missing-resource.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    dexOptions {
        dexInProcess true
        maxProcessCount 2
        javaMaxHeapSize "2g"
        incremental true
    }

    defaultConfig {
        applicationId "app.we.go.movies"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // for circleCI
        DdmPreferences.setTimeOut(60000)
        // Enabling multidex support.
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            // Run code coverage reports by default on debug builds.
            testCoverageEnabled = true
        }


        buildTypes.each {
            it.buildConfigField 'String', 'TMDB_API_KEY', "\"$System.env.TMDB_API_KEY\""
        }
    }

    adbOptions {
        timeOutInMs 600000 // set timeout to 1 minute
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            testInstrumentationRunner = "app.we.go.movies.MockJUnitRunner"
        }
        prod {
        }
        instantRun {
            minSdkVersion 21
            targetSdkVersion 23
        }
    }

    // Remove mockRelease as it's not needed.
    //
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('instantRun')) {
            variant.setIgnore(true);
        }

    }


    sourceSets {
        String sharedTestDir = 'src/testCommon/java'
        androidTest {
            java.srcDirs += sharedTestDir
        }

        test {
            java.srcDirs += sharedTestDir
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

/*
Resolves dependency versions across test and production APKs, specifically, transitive
dependencies. This is required since Espresso internally has a dependency on support-annotations.
*/
configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:23.1.1"
    resolutionStrategy.force "com.android.support:support-v4:23.1.1"

}

repositories {
    mavenCentral()

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    apt 'com.google.dagger:dagger-compiler:2.0'
    compile 'com.google.dagger:dagger:2.0'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.google.code.gson:gson:2.2.4'
    // rx
    compile 'io.reactivex:rxandroid:1.2.0'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.5'
    // cupboard
    compile 'nl.qbusict:cupboard:2.1.4'
    // stetho
    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
    // picasso
    compile 'com.squareup.picasso:picasso:2.5.2'
    // retrofit
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.0.1'
    compile 'com.squareup.retrofit2:retrofit-mock:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    // Butterknife
    compile 'com.jakewharton:butterknife:7.0.1'
    // multidex
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.android.support.test.espresso:espresso-idling-resource:2.2.1'
    // widgets
    compile('com.github.yasevich:endless-recycler-view:1.1.0') {
    }
    // TESTING
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.squareup.retrofit2:retrofit-mock:2.0.2'
    androidTestCompile 'com.android.support:support-v4:23.1.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.1.1'
    // espresso
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-idling-resource:2.2.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.1'
    testCompile('com.squareup.assertj:assertj-android:1.1.1') {
        exclude module: 'support-annotations'
    }
    //   apt 'org.parceler:parceler:1.0.4'
}






